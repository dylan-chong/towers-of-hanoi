apply plugin: 'java'
apply plugin: 'eclipse' // For generating Eclipse projects
apply plugin: 'application'

version '1.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.2.30'

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'kotlin'

repositories {
    mavenCentral()
    maven {
        url "http://dl.bintray.com/jetbrains/spek"
    }
}

test {
    useJUnitPlatform {
        includeEngines 'junit-jupiter', 'spek'
    }
}

dependencies {
    // Include libraries in 'lib' directory
    compile fileTree(dir: 'lib', include: ['*.jar'])

    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile group: 'org.jetbrains.kotlin', name: 'kotlin-reflect', version: '1.2.30'

    testCompile 'org.jetbrains.spek:spek-api:1.1.5'
    testRuntime 'org.jetbrains.spek:spek-junit-platform-engine:1.1.5'

    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.1.0'
    testCompile 'org.junit.jupiter:junit-jupiter-api:5.1.0'
    testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.1.0'
}

run {
    standardInput = System.in
    enableAssertions = true
}
run.dependsOn assemble

mainClassName = 'main.MainKt'

// Submit task
def withDependenciesTag = '-with-dependencies'
def exportPath = './submit/'
def exportedJarFileName = 'app.jar'
task submit << {

    println "Exporting Java files and resources for assignment submission: $exportPath"

    // Copy jar
    copy {
        from './build/libs/'
        include "$project.name$withDependenciesTag*.jar"
        into exportPath
        rename { fileName ->
            if (fileName.endsWith(".jar")) {
                return exportedJarFileName
            }
            return fileName
        }
    }
}
submit.dependsOn assemble

// Export jar that includes the dependencies
// (copied from http://www.mkyong.com/gradle/gradle-create-a-jar-file-with-dependencies/)
task fatJar(type: Jar) {
    manifest {
        attributes(
                'Implementation-Title': 'Gradle Project',
                'Implementation-Version': version,
                'Main-Class': mainClassName,
        )
    }
    baseName = project.name + withDependenciesTag
    from {
        configurations.compile.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    with jar
}
jar.dependsOn fatJar

jar {
    // Copy Java files into jar
    from sourceSets.main.allSource
}

clean {
    delete exportPath
}
compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
